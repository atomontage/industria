#!/usr/bin/env scheme-script
;; -*- mode: scheme; coding: utf-8 -*- !#
;; Demonstration SSH client
;; Copyright © 2010, 2011 Göran Weinholt <goran@weinholt.se>
;;
;; This program is free software: you can redistribute it and/or modify
;; it under the terms of the GNU General Public License as published by
;; the Free Software Foundation, either version 3 of the License, or
;; (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful,
;; but WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
;; GNU General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.
#!r6rs

(import (rnrs)
        (only (srfi :13 strings) string-index)
        (srfi :39 parameters)
        (weinholt crypto dsa)
        (weinholt crypto rsa)
        (weinholt crypto sha-1)
        (weinholt crypto ssh-public-key (1))
        (weinholt net dns numbers)
        (weinholt net dns types)
        (weinholt net ssh)
        (weinholt net tcp)
        (weinholt text base64))

(define (print . x) (for-each display x) (newline))

(define (get-key hostname portname algorithm)
  (let-values (((in out) (tcp-connect hostname portname)))
    (parameterize ((preferred-server-host-key-algorithms (list algorithm)))
      (let ((conn (make-ssh-client in out)))
        (ssh-finish-key-exchange conn)
        (display "Server identification: ")
        (write (utf8->string (ssh-conn-peer-identification conn)))
        (newline)
        (close-ssh conn)
        (ssh-conn-host-key conn)))))

(define (print-key hostname key)
  (print (ssh-public-key-fingerprint key))
  (print (ssh-public-key-random-art key))
  (print key)
  (let ((key-bv (ssh-public-key->bytevector key)))
    (print hostname " "
           (cond ((rsa-public-key? key) "ssh-rsa ")
                 ((dsa-public-key? key) "ssh-dsa "))
           (base64-encode key-bv))
    ;; RFC4255
    (display "\tSSHFP\t")
    (dns-resource-print (current-output-port) 80
                        (make-dns-resource/SSHFP
                         '() 0 (dns-class IN)
                         (cond ((rsa-public-key? key) (dns-sshfp-algorithm RSA))
                               ((dsa-public-key? key) (dns-sshfp-algorithm DSA)))
                         (dns-sshfp-type SHA-1)
                         (sha-1->bytevector (sha-1 key-bv))))
    (newline)))


(define (main hostname portname)
  (print-key hostname (get-key hostname portname "ssh-dss"))
  (print-key hostname (get-key hostname portname "ssh-rsa")))

;; This is very incomplete. It should do userauth etc for a proper
;; demo.

(apply
 (case-lambda
   ((who hostname portname)
    (main hostname portname))
   ((who hostname)
    (main hostname "22"))
   ((who)
    (print "Usage: " who " hostname [port]")
    (exit 1)))
 (command-line))
